/**
 * Jacoco gradle settings for test coverage.
 */

def fileWithCoverageForAllTests = "$buildDir/jacoco/coverageForAllTests.exec"

jacoco {
	toolVersion = "0.8.3"
}

sonarqube {
	properties {
		properties["sonar.jacoco.reportPath"] = fileWithCoverageForAllTests
		properties["sonar.sources"] = files(sourceSets.main.java.srcDirs)
		properties["sonar.junit.reportPaths"] = files("$buildDir/test-results/dbIntegrationTest", "$buildDir/test-results/test")
	}
}

task jacocoUnitTestReport(type: JacocoReport) {

	reports {
		xml.enabled false
		csv.enabled false
		html.destination file("${buildDir}/jacocoUnitTestHtml")
	}

	executionData = files("$buildDir/jacoco/test.exec")
	classDirectories = files(sourceSets.main.java.outputDir)
	sourceDirectories = files(sourceSets.main.java.srcDirs)

	dependsOn test
}

task jacocoDbIntegrationTestReport(type: JacocoReport) {

	reports {
		xml.enabled false
		csv.enabled false
		html.destination file("${buildDir}/jacocoDbIntegrationTestHtml")
	}

	executionData = files("$buildDir/jacoco/dbIntegrationTest.exec")
	classDirectories = files(sourceSets.main.java.outputDir)
	sourceDirectories = files(sourceSets.main.java.srcDirs)

	dependsOn dbIntegrationTest
}

task jacocoMerge(type: JacocoMerge) {

	executionData = files("$buildDir/jacoco/test.exec", "$buildDir/jacoco/dbIntegrationTest.exec")
	destinationFile = file(fileWithCoverageForAllTests)

	dependsOn test, dbIntegrationTest
}

task jacocoAllTestReport(type: JacocoReport) {

	reports {
		xml.enabled false
		csv.enabled false
		html.destination file("${buildDir}/jacocoAllTestHtml")
	}

	executionData = files(fileWithCoverageForAllTests)
	classDirectories = files(sourceSets.main.java.outputDir)
	sourceDirectories = files(sourceSets.main.java.srcDirs)

	dependsOn tasks.jacocoMerge
}

tasks.sonarqube.dependsOn jacocoMerge