/**
 * Contains tasks that run database integration tests.
 */

def dbIntegrationTestSourceDir = 'src/dbIntegrationTest/java'

sourceSets {
    dbIntegrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file(dbIntegrationTestSourceDir)
        }
    }
}

configurations {
    dbIntegrationTestCompile.extendsFrom testCompile
    dbIntegrationTestRuntime.extendsFrom testRuntime
}

/**
 * Runs all database integration tests.
 */
task dbIntegrationTest(type: Test) {

    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = "Runs all database integration tests"

    dependsOn 'startPostgresInstance'
    finalizedBy 'stopPostgresInstance'

    testClassesDirs = sourceSets.dbIntegrationTest.output.classesDirs
    classpath = sourceSets.dbIntegrationTest.runtimeClasspath

    outputs.upToDateWhen { false }

    mustRunAfter test
}

/**
 * Prepares the machine for running database integration tests from Intellij
 */
task dbIntegrationTestInteractive(type: Exec) {

    commandLine 'docker', 'run', '--rm', '--name', 'dbIntegrationTestPostgresContainer', '-e', 'POSTGRESS_PASSWORD=dbIntegrationTest', '-e', 'POSTGRES_USER=tiolktrack', '-p', '5432:5432', 'postgres'
}

task startPostgresInstance(type: Exec) {

    commandLine 'docker', 'run', '--rm', '--name', 'dbIntegrationTestPostgresContainer', '-e', 'POSTGRESS_PASSWORD=dbIntegrationTest', '-e', 'POSTGRES_USER=tiolktrack', '-p', '5432:5432', '-d', 'postgres'
}

task stopPostgresInstance(type: Exec) {
    commandLine 'docker', 'stop', 'dbIntegrationTestPostgresContainer'
}

check.dependsOn dbIntegrationTest

idea {
    module {
        testSourceDirs += file(dbIntegrationTestSourceDir)
    }
}