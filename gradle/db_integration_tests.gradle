/**
 * Contains tasks that run database integration tests.
 */

sourceSets {
	dbIntegrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir(file("$projectDir/src/dbIntegrationTest/java"))
		}

		resources {
			srcDirs "$projectDir/src/dbIntegrationTest/resources"
		}
	}
}

configurations {
	dbIntegrationTestCompile.extendsFrom testCompile
	dbIntegrationTestRuntime.extendsFrom testRuntime
}

task copyDbIntegrationTestResources(type: Copy) {

	from("$projectDir/db") {
		include 'liquibase/'
	}
	into sourceSets.dbIntegrationTest.output.resourcesDir
}

task dbIntegrationTest(type: Test) {

	group = LifecycleBasePlugin.VERIFICATION_GROUP
	description = "Runs all database integration tests"

	doFirst {
		if (project.hasProperty('db.integration.test.network')) {
			println('Adding a network to newly created docker container')
			exec {
				commandLine 'docker', 'run', '--rm', '--name', 'dbIntegrationTestPostgresContainer', '-e', 'POSTGRESS_PASSWORD=dbIntegrationTest', '-e', 'POSTGRES_USER=tiolktrack', '-d', '-p', '5432:5432', '--network', project.getProperties().get('db.integration.test.network'), 'postgres'
			}
		} else {
			println('Creating postgres container without a custom network')
			exec {
				commandLine 'docker', 'run', '--rm', '--name', 'dbIntegrationTestPostgresContainer', '-e', 'POSTGRESS_PASSWORD=dbIntegrationTest', '-e', 'POSTGRES_USER=tiolktrack', '-d', '-p', '5432:5432', 'postgres'
			}
		}
	}

	doLast {
		exec {
			commandLine 'bash', '-c', 'if [[ $(docker container ls -f "name=dbIntegrationTestPostgresContainer" -q | grep . -q; echo $?) -eq 0 ]] ; then docker stop dbIntegrationTestPostgresContainer ; fi'
		}
	}

	testClassesDirs = sourceSets.dbIntegrationTest.output.classesDirs
	classpath = sourceSets.dbIntegrationTest.runtimeClasspath

	dependsOn copyDbIntegrationTestResources
	mustRunAfter test
}

task dbIntegrationTestIntellij(type: Exec) {

	group = LifecycleBasePlugin.VERIFICATION_GROUP
	description = "Allows running database integration tests in Intellij"

	commandLine 'docker', 'run', '--rm', '--name', 'dbIntegrationTestPostgresContainer', '-e', 'POSTGRESS_PASSWORD=dbIntegrationTest', '-e', 'POSTGRES_USER=tiolktrack', '-p', '5432:5432', 'postgres'

	dependsOn += copyDbIntegrationTestResources
}

idea {
	module {
		testSourceDirs += sourceSets.dbIntegrationTest.java.srcDirs
		testResourceDirs += sourceSets.dbIntegrationTest.resources.srcDirs
		testResourceDirs += sourceSets.dbIntegrationTest.output.resourcesDir
	}
}

check.dependsOn dbIntegrationTest
